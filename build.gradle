plugins {
    id 'java'
    id 'idea'
    id 'fabric-loom' version '1.4-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
archivesBaseName = project.archives_base_name
group = project.maven_group

dependencies {
    afterEvaluate {
        subprojects.each {
            if (it.name.startsWith("creo")) {
                dependencies.implementation project(path: ":${it.name}", configuration: "dev")
                dependencies.include project(":${it.name}")
            }
        }
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'fabric-loom'
    apply plugin: 'maven-publish'

    it.version = it.mod_version + "+" + minecraft_version
    it.group = maven_group

    repositories {
        maven {
            url 'https://jitpack.io'
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        withSourcesJar()
        withJavadocJar()
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release.set(17)
        it.options.encoding = "UTF-8"
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"
        modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"
        modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}"
    }

    Project sub = it
    tasks.withType(ProcessResources).configureEach {
        inputs.property "version", sub.version
        inputs.property "loader_version", rootProject.loader_version
        inputs.property "minecraft_version", rootProject.minecraft_version

        filesMatching("fabric.mod.json") {
            expand "version": sub.version,
                    "loader_version": rootProject.loader_version,
                    "minecraft_version": rootProject.minecraft_version
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
    }

    configurations {
        dev
    }

    artifacts {
        dev jar
    }
}

subprojects.forEach {
    it.group = project.maven_group
    it.version = it.mod_version
}

jar {
    destinationDirectory = file("$rootDir/build/libs/$minecraft_version/$mod_version")
}

javadocJar {
    destinationDirectory = file("$rootDir/build/libs/$minecraft_version/$mod_version")
}

sourcesJar {
    destinationDirectory = file("$rootDir/build/libs/$minecraft_version/$mod_version")
}
